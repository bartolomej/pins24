program -> definitions .

definitions -> definition definitions_rest .
definitions_rest -> definitions .
definitions_rest -> .

definition -> fun_definition .
definition -> var_definition .

fun_definition -> fun identifier "(" parameters ")" fun_definition_rest .
fun_definition_rest -> "=" statements .
fun_definition_rest -> .

parameters -> identifier parameters_rest .
parameters_rest -> "," parameters .
parameters_rest -> .

var_definition -> var identifier "=" initializers .

initializers -> initializer initializers_rest .
initializers_rest -> "," initializers .
initializers_rest -> .

initializer -> int_const initializer_rest .
initializer -> char_const .
initializer -> string_const .
initializer_rest -> "*" const .
initializer_rest -> .

const -> int_const | char_const | string_const .

statements -> statement statements_rest .
statements_rest -> "," statements .
statements_rest -> .

statement -> expression_or_assignment | if_statement | while_statement | let_statement .

if_statement -> "if" expression "then" statements optional_else "end" .
optional_else -> "else" statements .
optional_else -> .

while_statement -> "while" expression "do" statements "end" .

let_statement -> "let" definitions "in" statements "end" .

expression_or_assignment -> expression optional_assignment .
optional_assignment -> "=" expression .
optional_assignment -> .

expression -> disjunction .

disjunction -> conjunction disjunction_1 .
disjunction_1 -> "||" conjunction .
disjunction_1 -> .
conjunction -> comparison conjunction_1 .
conjunction_1 -> "&&" comparison .
conjunction_1 -> .
comparison -> addition comparison_1 .
comparison_1 -> "==" addition .
comparison_1 -> "!=" addition .
comparison_1 -> "<" addition .
comparison_1 -> ">" addition .
comparison_1 -> "<=" addition .
comparison_1 -> ">=" addition .
comparison_1 -> .
addition -> multiplication addition_1 .
addition_1 -> "+" multiplication addition_1 .
addition_1 -> "-" multiplication addition_1 .
addition_1 -> .
multiplication -> prefix multiplication_1.
multiplication_1 -> "*" prefix multiplication_1 .
multiplication_1 -> "/" prefix multiplication_1 .
multiplication_1 -> "%" prefix multiplication_1 .
multiplication_1 -> .
prefix -> "!" postfix .
prefix -> "+" postfix .
prefix -> "-" postfix .
prefix -> "^" postfix .
prefix -> postfix .
postfix -> const_or_grouping postfix_1 .
postfix_1 -> "^" .
postfix_1 -> .
const_or_grouping -> "(" expression ")" .
const_or_grouping -> const .
const_or_grouping -> function_call_or_variable_access .

function_call_or_variable_access -> identifier optional_function_call .
optional_function_call -> "(" arguments ")" .
optional_function_call -> .
arguments -> expression arguments_1 .
arguments -> .
arguments_1 -> "," expression arguments_1 .
arguments_1 -> .